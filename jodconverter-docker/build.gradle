import java.text.SimpleDateFormat

plugins {
    id "eu.xenit.docker" version "4.0.3"
}

configurations {
    jodConverterWar
}

dependencies {
    jodConverterWar project(path: ':jodconverter-samples:jodconverter-sample-spring-boot', configuration: 'myWar')
}


def dockerStagingDir = "${buildDir}/docker/"

println "Environment variables=" + System.env

def branchName = System.env.TRAVIS_BRANCH

dockerFile {
    dockerFile = file(dockerStagingDir + 'Dockerfile')
    dockerBuild {
        repository = "xenit/jodconverter"
        def buildDate = new SimpleDateFormat('yyyyMMddHHmm').format(new Date())
        automaticTags = false
        tags = ["${project.version}", branchName+'-build-'+buildDate]
        println "Built tags are " + tags
    }
}

docker {
    registryCredentials {
        username = System.getenv("DOCKER_USER")
        password = System.getenv("DOCKER_PASSWORD")
    }
}


task stageDockerDeps(type: Copy) {
    from configurations.jodConverterWar
    from 'src/main/docker'
    into dockerStagingDir
}

afterEvaluate {
    buildDockerImage.dependsOn stageDockerDeps
}

buildDockerImage {
    buildArgs = [
            'JODCONVERTER_WAR_NAME': "${configurations.jodConverterWar.singleFile.getName()}"
    ]
}

dockerCompose {
    useComposeFiles = ['src/test/compose/docker-compose.yml']
    captureContainersOutput = true
    removeVolumes = true
}
