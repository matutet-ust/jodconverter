import java.text.SimpleDateFormat

plugins {
    id "eu.xenit.docker" version "4.0.3"
    id "be.vbgn.ci-detect" version "0.3.0"
}

configurations {
    jodConverterWar
}

dependencies {
    jodConverterWar project(path: ':jodconverter-samples:jodconverter-sample-rest', configuration: 'myWar')
}


def dockerStagingDir = "${buildDir}/docker/"

dockerFile {
    dockerFile = file(dockerStagingDir + 'Dockerfile')
    dockerBuild {
        repository = "xenit/jodconverter"
        def buildDate = new SimpleDateFormat('yyyyMMddHHmm').format(new Date())
        automaticTags = false
        tags = ["${project.version}"]
        if(ci.isCi()) {
            tags+=[ci.branch + "-build-" + buildDate]
        }
    }
}

docker {
    registryCredentials {
        username = System.getenv("DOCKER_USER")
        password = System.getenv("DOCKER_PASSWORD")
    }
}


task stageDockerDeps(type: Copy) {
    from configurations.jodConverterWar
    from 'src/main/docker'
    into dockerStagingDir
}

afterEvaluate {
    buildDockerImage.dependsOn stageDockerDeps
}

buildDockerImage {
    buildArgs = [
            'JODCONVERTER_WAR_NAME': "${configurations.jodConverterWar.singleFile.getName()}"
    ]
}

dockerCompose {
    useComposeFiles = ['src/test/compose/docker-compose.yml']
    captureContainersOutput = true
    removeVolumes = true
}
